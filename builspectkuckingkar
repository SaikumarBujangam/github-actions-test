data "aws_codecommit_repository" "repo" {
  repository_name = var.repository_name
}

#####################################################
# Creating cloudwatch-event-rule to trigger pipeline
#####################################################
resource "aws_cloudwatch_event_rule" "trigger_pipeline_on_repo_updates" {

  name        = "${var.environment}-${var.repository_name}-${var.default_branch}-pipeline-rule"
  description = "Amazon CloudWatch Events rule to automatically start the ${var.environment} pipeline when a change occurs in the ${var.repository_name} repository and ${var.default_branch} branch. "

  event_pattern = <<EOF
{
  "source": ["aws.codecommit"],
  "detail-type": ["CodeCommit Repository State Change"],
  "resources": ["${data.aws_codecommit_repository.repo.arn}"],
  "detail": {
    "event": ["referenceCreated", "referenceUpdated"],
    "referenceType": ["branch"],
    "referenceName": ["${var.default_branch}"]
  }
}
EOF
}

resource "aws_cloudwatch_event_target" "codepipeline" {

  rule      = aws_cloudwatch_event_rule.trigger_pipeline_on_repo_updates.name
  target_id = "TriggerPipeline"
  arn       = aws_codepipeline.codepipeline.arn
  role_arn  = var.cloudwatch_event_role_arn
}
